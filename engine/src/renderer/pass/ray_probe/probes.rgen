#version 460
#extension GL_EXT_ray_tracing : require
#extension GL_GOOGLE_include_directive : enable
#extension GL_EXT_scalar_block_layout : enable

#include "descriptors.glsl"
#include "../common/rand.glsl"
#include "probes.glsl"

layout(location = 0) rayPayloadEXT PrimaryHitPayload prd;

layout(binding = 0, set = 0) uniform accelerationStructureEXT tlas;
layout(binding = 5, set = 1, rgba32f) uniform restrict writeonly image2D new_probes;

void main()
{
    const uvec4 co = uvec4(gl_LaunchIDEXT.xy, globals.frame, 0);
    const uint probe_index = gl_LaunchIDEXT.x;
    const vec3 probe_cell_size = probe_cell_size();

    const uvec3 probe = uvec3(
        probe_index % globals.probes_extent.x,
        (probe_index / globals.probes_extent.x) % globals.probes_extent.y,
        (probe_index / globals.probes_extent.x / globals.probes_extent.y) % globals.probes_extent.z
    );

    const vec3 origin = probe_cell_size * probe + globals.probes_offset;
    vec3 dir = probe_rand(co);
    prd.result = vec3(0);
    prd.shadow_rays = 1;
    traceRayEXT(tlas, 0, 0xff, 0, 0, 0, origin, 0.0, dir, 1000.0, 0);

    imageStore(new_probes, ivec2(gl_LaunchIDEXT.xy), vec4(prd.result, 1.));
}
